<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
 	        xmlns:axsl="http://www.w3.org/1999/XSL/TransformAlias"
	        xmlns:sch="http://www.ascc.net/xml/schematron"
	        xmlns:iso="http://purl.oclc.org/dsdl/schematron"
                version="1.0">
<!--
     This specializes iso-schematron-skeleton.xsl to analyze all assertions
     and abort with a message of all violations.

 $Id: Message-Schematron-terminator.xsl,v 1.5 2008/04/15 08:12:11 G. Ken Holman Exp $

 Copyright (C) - Crane Softwrights Ltd.
               - http://www.CraneSoftwrights.com/links/res-dev.htm
 
 Redistribution and use in source and binary forms, with or without
 modification, are permitted provided that the following conditions are met:
 
 - Redistributions of source code must retain the above copyright notice,
   this list of conditions and the following disclaimer.
 - Redistributions in binary form must reproduce the above copyright notice,
   this list of conditions and the following disclaimer in the documentation
   and/or other materials provided with the distribution.
 - The name of the author may not be used to endorse or promote products
   derived from this software without specific prior written permission.
 
 THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
 IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN
 NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED
 TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
 PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
 LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
 NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE,
 EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 
 Note: for your reference, the above is the "Modified BSD license", this text
     was obtained 2003-07-26 at http://www.xfree86.org/3.3.6/COPYRIGHT2.html#5
-->

<!--specialize the published Schematron reference implementation-->
<xsl:import href="iso_schematron_skeleton_for_xslt1.xsl"/>

<!--augment with methodology-specific process additions-->
<xsl:template match="node()" mode="stylesheetbody">
  <xsl:comment>Importing stylesheet additions</xsl:comment>
  <axsl:output method="text"/>

  <!--create the reference portion of the stylesheet-->
  <xsl:apply-imports/>
</xsl:template>

<!--this overrides the skeleton handling the root node-->
<xsl:template name="process-root">
  <xsl:param name="contents"/>
  <xsl:comment>Root node processing collects all assertions</xsl:comment>
  <!--first determine the text result of the imported stylesheet-->
  <axsl:variable name="result">
    <xsl:copy-of select="$contents"/>
  </axsl:variable>
  <!--then abend if the end result has non-white-space text in it-->
  <axsl:if test="normalize-space(string($result))">
    <axsl:message terminate="yes">
      <axsl:value-of select="string($result)" disable-output-escaping="yes"/>
    </axsl:message>
  </axsl:if>
</xsl:template>

<!--process a message generated by a failed assertion-->
<xsl:template name="process-message">
  <!--express the message information-->
  <xsl:apply-templates mode="text"/>
  <!--follow it with an XPath address-->
  <axsl:text>: </axsl:text>
  <axsl:apply-templates select="." mode="schematron-get-full-path-3"/>
  <axsl:text xml:space="preserve">
</axsl:text>
</xsl:template>

</xsl:stylesheet>